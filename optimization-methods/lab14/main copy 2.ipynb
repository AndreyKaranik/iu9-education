{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "dc827389",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "Лучший путь: [1, 6, 5, 7, 4, 10, 9, 2, 3, 8]\n",
      "Длина пути: 3.205272051291517\n"
     ]
    }
   ],
   "source": [
    "using Random, LinearAlgebra, Distributions\n",
    "\n",
    "struct AntSystemParams\n",
    "    α::Float64\n",
    "    β::Float64\n",
    "    Q::Float64\n",
    "    ρ::Float64\n",
    "    τ₀::Float64\n",
    "    num_ants::Int\n",
    "    max_iterations::Int\n",
    "end\n",
    "\n",
    "function generate_distance_matrix(n::Int; seed=123)\n",
    "    Random.seed!(seed)\n",
    "    cities = rand(n, 2)\n",
    "    d = zeros(n, n)\n",
    "    for i in 1:n\n",
    "        for j in i+1:n\n",
    "            dist = norm(cities[i, :] - cities[j, :])\n",
    "            d[i, j] = dist\n",
    "            d[j, i] = dist\n",
    "        end\n",
    "    end\n",
    "    return d\n",
    "end\n",
    "\n",
    "function construct_path(n::Int, d::Matrix{Float64}, τ::Matrix{Float64}, α::Float64, β::Float64)\n",
    "    path = Int[]\n",
    "    visited = falses(n)\n",
    "    start = rand(1:n)\n",
    "    push!(path, start)\n",
    "    visited[start] = true\n",
    "    current = start\n",
    "    \n",
    "    for _ in 2:n\n",
    "        available = findall(.!visited)\n",
    "        if isempty(available)\n",
    "            break\n",
    "        end\n",
    "        \n",
    "        probabilities = zeros(length(available))\n",
    "        for (idx, city) in enumerate(available)\n",
    "            η = 1.0 / d[current, city]\n",
    "            probabilities[idx] = (τ[current, city]^α) * (η^β)\n",
    "        end\n",
    "        \n",
    "        total = sum(probabilities)\n",
    "        if total ≈ 0.0\n",
    "            next_city = rand(available)\n",
    "        else\n",
    "            probabilities ./= total\n",
    "            next_city = available[rand(Categorical(probabilities))]\n",
    "        end\n",
    "        \n",
    "        push!(path, next_city)\n",
    "        visited[next_city] = true\n",
    "        current = next_city\n",
    "    end\n",
    "    return path\n",
    "end\n",
    "\n",
    "function calculate_path_length(path::Vector{Int}, d::Matrix{Float64})\n",
    "    total_length = 0.0\n",
    "    n = length(path)\n",
    "    for i in 1:n\n",
    "        city_i = path[i]\n",
    "        city_j = (i < n) ? path[i+1] : path[1]\n",
    "        total_length += d[city_i, city_j]\n",
    "    end\n",
    "    return total_length\n",
    "end\n",
    "\n",
    "function update_pheromones!(τ::Matrix{Float64}, all_paths::Vector{Vector{Int}}, all_lengths::Vector{Float64}, Q::Float64, ρ::Float64)\n",
    "    τ .*= (1 - ρ)\n",
    "    for (path, L) in zip(all_paths, all_lengths)\n",
    "        if L ≈ 0.0\n",
    "            continue\n",
    "        end\n",
    "        Δτ = Q / L\n",
    "        m = length(path)\n",
    "        for i in 1:m\n",
    "            city_i = path[i]\n",
    "            city_j = (i < m) ? path[i+1] : path[1]\n",
    "            τ[city_i, city_j] += Δτ\n",
    "            τ[city_j, city_i] += Δτ\n",
    "        end\n",
    "    end\n",
    "end\n",
    "\n",
    "function ant_system(d::Matrix{Float64}; params::AntSystemParams)\n",
    "    n = size(d, 1)\n",
    "    τ = fill(params.τ₀, n, n)\n",
    "    best_path = Int[]\n",
    "    best_length = Inf\n",
    "    \n",
    "    for _ in 1:params.max_iterations\n",
    "        all_paths = Vector{Vector{Int}}()\n",
    "        all_lengths = Vector{Float64}()\n",
    "        \n",
    "        for _ in 1:params.num_ants\n",
    "            path = construct_path(n, d, τ, params.α, params.β)\n",
    "            L = calculate_path_length(path, d)\n",
    "            push!(all_paths, path)\n",
    "            push!(all_lengths, L)\n",
    "            \n",
    "            if L < best_length\n",
    "                best_length = L\n",
    "                best_path = copy(path)\n",
    "            end\n",
    "        end\n",
    "        \n",
    "        update_pheromones!(τ, all_paths, all_lengths, params.Q, params.ρ)\n",
    "    end\n",
    "    \n",
    "    return best_path, best_length\n",
    "end\n",
    "\n",
    "params = AntSystemParams(1.0, 2.0, 100.0, 0.1, 0.1, 10, 100)\n",
    "\n",
    "n = 10\n",
    "d = generate_distance_matrix(n)\n",
    "best_path, best_length = ant_system(d, params=params)\n",
    "\n",
    "println(\"Лучший путь: \", best_path)\n",
    "println(\"Длина пути: \", best_length)"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.10.5",
   "language": "julia",
   "name": "julia-1.10"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.10.5"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
